/// Formats a string by replacing placeholders `{}` with provided arguments.
///
/// # Parameters
/// - `msg`: The message containing `{}` placeholders to be replaced.
/// - `args`: The arguments to format into the placeholders.
///
/// Curly braces can be escaped by using `{{` and `}}`. If no arguments are provided for a placeholder, it will be left as `{}`.
///
/// # Examples
/// ```
/// let result = format("Hello, {}!", "world"); // "Hello, world!"
/// let escaped = format("This is a brace: {{}}"); // "This is a brace: {}"
/// ```
pub fn format(msg: string, ...args: anytype) -> string {
    let str = "";
    let chars = msg.chars();
    let last = 0;

    let i = 0;
    while i < chars.len() {
        if chars[i] == "{" && chars[i + 1] == "{" {
            str += "{";
            i += 2;
        } else if chars[i] == "}" && chars[i + 1] == "}" {
            str += "}";
            i += 2;
        } else if chars[i] == "{" && chars[i + 1] == "}" {
            i += 2;
            if args[last] == null {
                str += "{}";
            } else {
                str += __format(args[last]);
                last += 1;
            }
        } else {
            str += chars[i];
            i += 1;
        }
    }

    return str;
}
